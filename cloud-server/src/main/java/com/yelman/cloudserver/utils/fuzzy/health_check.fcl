FUNCTION_BLOCK cattle_health

VAR_INPUT
    temperature : REAL;
    heart_rate : REAL;
    rumination : REAL;
    humidity : REAL;
END_VAR

VAR_OUTPUT
    disease_risk : REAL;
END_VAR

FUZZIFY temperature
    TERM very_low := (33,1) (34,1) (35,0);
    TERM low := (34.5,0) (36.5,1) (37.5,0);
    TERM normal := (36.5,0) (38.5,1) (40,0);
    TERM high := (39.0,0) (40.5,1) (42,0);
    TERM very_high := (41.0,0) (42.5,1) (44,1);
END_FUZZIFY

FUZZIFY heart_rate
    TERM very_low := (20,1) (30,1) (40,0);
    TERM low := (35,0) (45,1) (55,0);
    TERM normal := (50,0) (70,1) (90,0);
    TERM high := (80,0) (100,1) (120,0);
    TERM very_high := (110,0) (130,1) (140,1);
END_FUZZIFY

FUZZIFY rumination
    TERM low := (100,1) (200,1) (300,0);
    TERM normal := (250,0) (360,1) (480,0);
    TERM high := (450,0) (600,1) (700,1);
END_FUZZIFY

FUZZIFY humidity
    TERM very_low := (10,1) (20,1) (40,0);
    TERM low := (35,0) (50,1) (60,0);
    TERM normal := (55,0) (70,1) (80,0);
    TERM high := (75,0) (85,1) (90,0);
    TERM very_high := (85,0) (95,1) (100,1);
END_FUZZIFY

DEFUZZIFY disease_risk
    TERM very_low := (0, 1) (10, 1);
    TERM low := (10, 0) (30, 1);
    TERM medium := (30, 0) (60, 1);
    TERM high := (60, 0) (80, 1);
    TERM very_high := (80, 0) (100, 1);

    METHOD : COG;

END_DEFUZZIFY

RULEBLOCK RuleBase
    AND : MIN;
    ACT : MIN;
    ACCU : MAX;

    RULE 1: IF temperature IS high OR heart_rate IS high THEN disease_risk IS high;
    RULE 2: IF temperature IS very_high AND heart_rate IS very_high THEN disease_risk IS very_high;
    RULE 3: IF temperature IS low AND rumination IS normal THEN disease_risk IS very_low;
    RULE 4: IF temperature IS very_low AND heart_rate IS very_low THEN disease_risk IS medium;
    RULE 5: IF humidity IS very_high AND heart_rate IS high THEN disease_risk IS high;
    RULE 6: IF rumination IS low AND temperature IS high THEN disease_risk IS high;
    RULE 7: IF rumination IS normal AND temperature IS normal AND heart_rate IS normal THEN disease_risk IS very_low;
    RULE 8: IF humidity IS very_high AND temperature IS very_high THEN disease_risk IS very_high;
    RULE 9: IF rumination IS high AND temperature IS normal THEN disease_risk IS very_low;
    RULE 10: IF rumination IS low AND heart_rate IS high THEN disease_risk IS high;

    // Ek: Herhangi bir durumda sonuç garantisi için default rules
    RULE 11: IF temperature IS normal THEN disease_risk IS low;
    RULE 12: IF heart_rate IS normal THEN disease_risk IS low;
    RULE 13: IF rumination IS normal THEN disease_risk IS low;
    RULE 14: IF humidity IS normal THEN disease_risk IS low;

END_RULEBLOCK
END_FUNCTION_BLOCK
